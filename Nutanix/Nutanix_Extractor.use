# Nutanix Endpoint ( IP/hostname)
public var nutanix_endpoint = "10.0.0.1"
# Nutanix Endpoint port number (default: 9440)
public var nutanix_port = "9440"
# Nutanix API UserName
public var api_user = "apiuser"
# Nutanix API Password
public var api_password = "api_password"
# Directory where data is exported to
public var exportdir = "system/extracted/Nutanix"

# Setup Loglevel
loglevel INFO
# Setup API endpoint
var httproot = "https://${nutanix_endpoint}:${nutanix_port}/api/nutanix/v3"
# Setup JSON output folder
var jsonfolder = "${exportdir}/JSON"
# Setup JSON body folder
var bodyfolder = "${exportdir}/RequestBody"
# Setup output file
var export_file = "${exportdir}/${YEAR}${MONTH}${DAY}_VM_List.csv"

# Create output CSV file
csv "nutanix" = "${export_file}"
csv add_headers "nutanix" description entityname entityid vmname vmid ownername ownerid projectname projectid clustername clusterid type quantity unit
csv fix_headers "nutanix"

# Create Base64 Auth String
var authstring = "${api_user}:${api_password}"
encode base64 authstring
# Extract VM List
clear http_headers
set http_secure no
set http_header "Content-Type: application/json"
set http_header "Authorization: Basic ${authstring}"
set http_body data "{\"kind\":\"vm\",\"length\":999999}"
set http_savefile "${jsonfolder}/${YEAR}${MONTH}${DAY}_VM_List.json"
buffer nutanixvms = http POST "${httproot}/vms/list"
if (${HTTP_STATUS_CODE} != 200) {
	gosub write_http_error (${HTTP_STATUS_CODE})
}

# Extract items to the CSV Output
foreach $JSON{nutanixvms}.[entities] as this_entity {
	var description			= $JSON(this_entity).[spec].[description]
	var entityname			= $JSON(this_entity).[spec].[name]
	var entityid			= $JSON(this_entity).[metadata].[uuid]
	var vmname				= $JSON(this_entity).[spec].[name]
	var vmid				= $JSON(this_entity).[metadata].[uuid]
	var ownername 			= $JSON(this_entity).[metadata].[owner_reference].[name]
	var ownerid				= $JSON(this_entity).[metadata].[owner_reference].[uuid]
	var projectname			= $JSON(this_entity).[metadata].[project_reference].[name]
	var projectid			= $JSON(this_entity).[metadata].[project_reference].[uuid]
	var clustername			= $JSON(this_entity).[spec].[cluster_reference].[name]
	var clusterid			= $JSON(this_entity).[spec].[cluster_reference].[uuid]

	csv write_field nutanix ${description}
	csv write_field nutanix ${entityname}
	csv write_field nutanix "vm-${entityid}"
	csv write_field nutanix ${vmname}
	csv write_field nutanix ${vmid}
	csv write_field nutanix ${ownername}
	csv write_field nutanix ${ownerid}
	csv write_field nutanix ${projectname}
	csv write_field nutanix ${projectid}
	csv write_field nutanix ${clustername}
	csv write_field nutanix ${clusterid}
	csv write_field nutanix "VirtualMachine"
	csv write_field nutanix "1"
	csv write_field nutanix "vm"

	var quantity		= $JSON(this_entity).[spec].[resources].[num_sockets]

	csv write_field nutanix ${description}
	csv write_field nutanix "CPU"
	csv write_field nutanix "cpu-${entityid}"
	csv write_field nutanix ${vmname}
	csv write_field nutanix ${vmid}
	csv write_field nutanix ${ownername}
	csv write_field nutanix ${ownerid}
	csv write_field nutanix ${projectname}
	csv write_field nutanix ${projectid}
	csv write_field nutanix ${clustername}
	csv write_field nutanix ${clusterid}
	csv write_field nutanix "Processor"
	csv write_field nutanix ${quantity}
	csv write_field nutanix "cpu(s)"

	var quantity		= $JSON(this_entity).[spec].[resources].[memory_size_mib]

	csv write_field nutanix ${description}
	csv write_field nutanix "MEMORY"
	csv write_field nutanix "mem-${entityid}"
	csv write_field nutanix ${vmname}
	csv write_field nutanix ${vmid}
	csv write_field nutanix ${ownername}
	csv write_field nutanix ${ownerid}
	csv write_field nutanix ${projectname}
	csv write_field nutanix ${projectid}
	csv write_field nutanix ${clustername}
	csv write_field nutanix ${clusterid}
	csv write_field nutanix "Memory"
	csv write_field nutanix ${quantity}
	csv write_field nutanix "mib"

	foreach $JSON(this_entity).[spec].[resources].[disk_list] as this_disk {
		var entityname	= $JSON(this_disk).[device_properties].[device_type]
		var entityid	= $JSON(this_disk).[uuid]
		var quantity	= $JSON(this_disk).[disk_size_mib]

		csv write_field nutanix ${description}
		csv write_field nutanix ${entityname}
		csv write_field nutanix "disk-${entityid}"
		csv write_field nutanix ${vmname}
		csv write_field nutanix ${vmid}
		csv write_field nutanix ${ownername}
		csv write_field nutanix ${ownerid}
		csv write_field nutanix ${projectname}
		csv write_field nutanix ${projectid}
		csv write_field nutanix ${clustername}
		csv write_field nutanix ${clusterid}
		csv write_field nutanix "VirtualDisk"
		csv write_field nutanix ${quantity}
		csv write_field nutanix "mib"
	}
}

csv close "nutanix"


#### Subroutines ####
subroutine write_http_error {
	#Subroutine vars
	#${SUBARG_1} = HTTP_STATUS_CODE

	print Got HTTP status ${SUBARG_1}, expected a status of 200
	terminate with error
}